plugins {
    id 'com.android.library' version '4.1.1' apply false
    id 'org.jetbrains.kotlin.android' version '1.4.21-2' apply false
    id 'xyz.tynn.android.maven' version '0.1.0' apply false
    id 'com.github.ben-manes.versions' version '0.36.0'
    id 'xyz.tynn.idea.fix' version '0.1.2'
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
}

subprojects {
    group = 'xyz.tynn.astring'

    pluginManager.withPlugin('maven-publish') {
        apply from: "$rootDir/publishing.gradle"
    }

    pluginManager.withPlugin('com.android.library') {
        apply plugin: 'org.jetbrains.kotlin.android'
        apply from: "$rootDir/android.gradle"

        android {
            libraryVariants.all {
                it.generateBuildConfigProvider.configure {
                    enabled = false
                }
            }
        }

        dependencies {
            compileOnly 'org.jetbrains.kotlin:kotlin-stdlib'
        }

        tasks.withType(GenerateModuleMetadata).configureEach {
            enabled = false
        }
    }

    pluginManager.withPlugin('com.android.application') {
        apply from: "$rootDir/android.gradle"
    }

    tasks.withType(Test).configureEach {
        testLogging {
            events "failed"
            exceptionFormat "full"
            showCauses true
            showExceptions true
            showStackTraces true
        }
    }
}

dependencyUpdates {
    def isNonStable = { version ->
        ['alpha', 'beta', 'dev', 'rc', 'cr', 'm', 'preview', 'b', 'ea', 'eap'].any {
            version ==~ /(?i).*[.-]${it}[.\d-+]*.*/
        }
    }
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
    gradleReleaseChannel = 'current'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
