if (project != rootProject) publishing {
    apply plugin: 'xyz.tynn.android.maven'
    apply plugin: 'xyz.tynn.android.sources'
    apply plugin: 'signing'

    signing {
        final signingKey = System.env.SIGNING_KEY
        final signingPassword = System.env.SIGNING_PASSWORD
        useInMemoryPgpKeys(signingKey, signingPassword)
    }

    publications.all {
        signing.sign it
        pom {
            name = 'AString'
            description = 'A context aware string abstraction for Android'
            url = System.env.PROJECT_URL
            developers {
                developer {
                    id = 'tynn'
                    name = 'Christian Schmitz'
                    email = 'christian@tynn.xyz'
                }
            }
            licenses {
                license {
                    name = 'The Apache License, Version 2.0'
                    url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            scm {
                url = System.env.PROJECT_URL
            }
        }
    }
} else nexusPublishing {
    repositories {
        sonatype {
            final baseUrl = "https://s01.oss.sonatype.org"
            nexusUrl.set(uri("$baseUrl/service/local/"))
            snapshotRepositoryUrl.set(uri("$baseUrl/content/repositories/snapshots/"))
        }
    }
}
